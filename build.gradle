//
// Sloth: An Energy-Efficient Activity Recognition System
// Copyright 2016 Pejman Ghorbanzade <mail@ghorbanzade.com>
// Released under the terms of MIT License
// https://github.com/ghorbanzade/sloth/blob/master/LICENSE
//

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'pmd'

version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.google.guava:guava:19.0'
    compile 'commons-io:commons-io:2.5'
    compile 'commons-net:commons-net:3.4'
    compile 'log4j:log4j:1.2.17'
    compile 'org.rxtx:rxtx:2.1.7'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes('Implementation-Title': 'Sloth',
                   'Implementation-Version': version,
                   'Main-Class': 'com.ghorbanzade.sloth.SlothMain'
                   //'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    destinationDir = file("./doc/")
}

jacoco {
    toolVersion = '0.7.6.201602180812'
}

checkstyle {
    toolVersion = '6.17'
    ignoreFailures = false
}

pmd {
    toolVersion = '5.4.1'
    ignoreFailures = false
}

findbugs {
    ignoreFailures = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
        //html.stylesheet resources.text.fromFile(
        //        'config/findbugs/findbugs-color.xsl'
        //)
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

clean {
    delete "log"
}

check.dependsOn jacocoTestReport
